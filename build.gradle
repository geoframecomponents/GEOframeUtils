// include the BinTray repository
buildscript {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/palantir/releases" }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.palantir:gradle-gitsemver:0.7.0'
    }

}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'gitsemver'
// apply plugin: 'signing'

group = "com.github.geoframecomponents"
archivesBaseName = "GEOframeUtils"

def projectName = "GEOframeUtils"
def projectDescription = "Utilities for GEOframe developers"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = '1.8'

// to get project version from command line, just type
// $ gradle -PprojVersion=<version_number> build
// default is na
if (project.hasProperty('projVersion')) {
      project.version = project.projVersion
} else {
      project.version = 'na'
}

// name with versioning of the jar file
jar {
    baseName = projectName
    manifest {
        attributes 'Implementation-Title' : projectName,
                   'Implementation-Version': version
    }
}

repositories {

    maven {
        // jai_core 1.1.3
        // jai_codec 1.1.3
        url 'http://download.osgeo.org/webdav/geotools/'
    }

    maven {
        // hatbox 1.0.b7
        url 'http://maven.geo-solutions.it/'
    }

    // jgt-jgrassgears
    // jgt-oms3
    // jcip-annotations
    // commons-lang3
    mavenCentral()

    // local jar files
    flatDir {
        dirs 'lib'
    }
}

dependencies {

    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    compile group: 'net.jcip', name: 'jcip-annotations', version: '1.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'javax.media', name: 'jai_core', version: '1.1.3'
    compile group: 'javax.media', name: 'jai_codec', version: '1.1.3'
    compile group: 'org.jgrasstools', name: 'jgt-jgrassgears', version: '0.7.8'
    compile group: 'org.jgrasstools', name: 'jgt-oms3', version: '0.7.8'
    compile group: 'net.sourceforge.hatbox', name: 'hatbox', version: '1.0.b7'

    testCompile group: 'junit', name: 'junit', version: '4.10'

}

modifyPom {
    project {
        name 'GEOframeUtils'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Utils for GEOframe developers'
        url 'https://github.com/geoframecomponents/GEOframeUtils'

        scm {
          connection 'scm:git:https://github.com/geoframecomponents/GEOframeUtils.git'
          developerConnection 'scm:git:https://github.com/geoframecomponents/GEOframeUtils.git'
          url 'https://github.com/geoframecomponents/GEOframeUtils'
        }

        licenses {
          license {
            name 'General Public License V.3 (GPL3)'
            url 'http://www.gnu.org/licenses/gpl.html'
          }
        }

        developers {
          developer {
            id 'sidereus'
            name 'Francesco Serafin'
            email 'francesco.serafin.3@gmail.com'
          }
          developer {
            id 'ftt01'
            name 'Daniele Dalla Torre'
            email 'dallatorre.daniele@gmail.com'
          }
        }
    }
  }

nexus {
    sign = false
}

// bintray sync
if (hasProperty('bintrayUsername') ||
    System.getenv().containsKey('BINTRAY_USER')) {
    // Used by the bintray plugin
    bintray {
        user = System.getenv().getOrDefault('BINTRAY_USER', bintrayUsername)
        key = System.getenv().getOrDefault('BINTRAY_KEY', bintrayApiKey)
        publish = true

        pkg {
            repo = 'maven'
            name = projectName
            licenses = ['GPL-3.0']
            vcsUrl = 'https://github.com/geoframecomponents/GEOframeUtils.git'

            version {
                name = project.version
                desc = projectDescription
                released = new Date()

                mavenCentralSync {
                    user = System.getenv().getOrDefault('SONATYPE_USER',
                                                        nexusUsername)
                    password = System.getenv().getOrDefault('SONATYPE_PASSWORD',
                                                            nexusPassword)
                }
            }
        }

        configurations = ['archives']
    }
}
